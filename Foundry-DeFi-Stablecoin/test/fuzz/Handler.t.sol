//SPDX-License-Identifier: MIT

pragma solidity ^0.8.18;

import { Test } from "forge-std/Test.sol";
import { CSCEngine } from "../../src/CSCEngine.sol";
import { DecentralizedStableCoin } from "../../src/DecentralizedStableCoin.sol";
import { ERC20Mock } from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";

contract Handler is Test {
    CSCEngine cscEngine;
    DecentralizedStableCoin cscToken;
    ERC20Mock weth;
    ERC20Mock wbtc;

    uint256 public timesMintIsCalled;

    //Array que guarda a los usuarios que ya han depositado collateral.
    //Pusheamos al `msg.sender` en la funci칩n depositCollateral().
    address[] public usersWithCollateralDeposited;

    uint256 MAX_DEPOSIT_SIZED = type(uint96).max; //El valor m치ximo de uint96

    constructor(CSCEngine _cscEngine, DecentralizedStableCoin _cscToken) {
        cscEngine = _cscEngine;
        cscToken = _cscToken;

        address[] memory collateralTokens = cscEngine.getCollateralTokens();
        weth = ERC20Mock(collateralTokens[0]);
        wbtc = ERC20Mock(collateralTokens[1]);
    }

    //La explicaci칩n del desarrollo de estas funciones est치n en `Notes3.md` 游밻
    function mintCsc(uint256 _amountToMint, uint256 _addressSeed) public {
        if (usersWithCollateralDeposited.length == 0) {
            return;
        }
        address msgSender = usersWithCollateralDeposited[_addressSeed % usersWithCollateralDeposited.length];
        (uint256 totalCscMinted, uint256 collateralValueInUSD) = cscEngine.getAccountInformation(msgSender);

        // Usamos int aqu칤 porque no queremos numeros negativos
        int256 maxCscToMint = (int256(collateralValueInUSD) / 2) - int256(totalCscMinted);
        if (maxCscToMint < 0) {
            return;
        }

        _amountToMint = bound(_amountToMint, 0, uint256(maxCscToMint));
        if (_amountToMint == 0) {
            return;
        }

        vm.startPrank(msgSender);
        cscEngine.mintCsc(_amountToMint);
        vm.stopPrank();
        timesMintIsCalled++;
    }

    function depositCollateral(uint256 _collateralSeed, uint256 _amountCollateral) public {
        ERC20Mock collateral = _getCollateralFromSeed(_collateralSeed);

        //Queremos que el msg.sender deposite de 1 a type(96).max
        _amountCollateral = bound(_amountCollateral, 1, MAX_DEPOSIT_SIZED);

        vm.startPrank(msg.sender);
        collateral.mint(msg.sender, _amountCollateral);
        collateral.approve(address(cscEngine), _amountCollateral);
        cscEngine.depositCollateral(address(collateral), _amountCollateral);
        vm.stopPrank();
        usersWithCollateralDeposited.push(msg.sender);
    }

    function redeemCollateral(uint256 _collateralSeed, uint256 _amountCollateral) public {
        ERC20Mock collateral = _getCollateralFromSeed(_collateralSeed);
        uint256 maxCollateralToRedeem = cscEngine.getCollateralBalanceOfUsers(address(collateral), msg.sender);

        //Queremos que el msg.sender redima del 0 a la m치xima cantidad que tenga depositado en el protocolo.
        //Empezamos desde 0 porque si el m치xima valor es 0 se romper칤a si empezamos desde 1.
        _amountCollateral = bound(_amountCollateral, 0, maxCollateralToRedeem);
        if (_amountCollateral == 0) {
            return;
        }

        cscEngine.redeemCollateral(address(collateral), _amountCollateral);
    }

    /**
     * @notice la funci칩n elige din치micamente entre dos tokens ERC-20 simulados (weth o wbtc) bas치ndose en la paridad
     * de la semilla proporcionada. Si la semilla es par, devuelve el token asociado a weth; si es impar, devuelve el
     * token asociado a wbtc.
     * @param _collateralSeed Numero asociado al colateral.
     */
    function _getCollateralFromSeed(uint256 _collateralSeed) private view returns (ERC20Mock) {
        if (_collateralSeed % 2 == 0) {
            return weth;
        }
        return wbtc;
    }
}
